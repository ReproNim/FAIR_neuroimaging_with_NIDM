prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix prov: <http://www.w3.org/ns/prov#>
prefix ndar: <https://ndar.nih.gov/api/datadictionary/v2/dataelement/>
prefix nidm: <http://purl.org/nidash/nidm#>
prefix fs: <https://surfer.nmr.mgh.harvard.edu/>
prefix dct: <http://purl.org/dc/terms/>
prefix dctypes: <http://purl.org/dc/dcmitype/> 
prefix ilx: <http://uri.interlex.org/>

select distinct ?ID ?Sex ?Age ?brainvol_softwareLabel ?brainvol

where {
    # tool initialization                  
    ?tool_act a prov:Activity ;
                prov:qualifiedAssociation [prov:agent [nidm:NIDM_0000164 ?tool]] .
    ?tool_act prov:qualifiedAssociation [prov:agent [ndar:src_subject_id ?ID]] .

         
    # Segmented Brain Volume   
    ?tool_entity prov:wasGeneratedBy ?tool_act ;
            ?measure_brainvol ?brainvol .      
            ?measure_brainvol a/rdfs:subClassOf* nidm:DataElement ;
                    rdfs:label ?brainvol_softwareLabel;
                    #fs:structure 'EstimatedTotalIntraCranialVol'; # eICV
                    fs:structure 'BrainSeg'; #Brain segmentation
                    nidm:measureOf <http://uri.interlex.org/base/ilx_0112559>; # Volume
                    nidm:datumType <http://uri.interlex.org/base/ilx_0738276>. # Scalar
            ?as_activity prov:qualifiedAssociation [prov:agent [ndar:src_subject_id ?ID]] . 

    # find sex data element uuid
    {?sex_measure a nidm:PersonalDataElement ;
        nidm:isAbout <http://uri.interlex.org/base/ilx_0101292> . #Biological sex
    }

    # find age data element uuid
    {?age_measure a nidm:PersonalDataElement ;
        nidm:isAbout ilx:ilx_0100400 . #Age    
    }

    ?as_entity prov:wasGeneratedBy ?as_activity ;
        ?sex_measure ?SexCoded ;
        ?age_measure ?Age.    
    bind(IF((?SexCoded ="M" || ?SexCoded ="Male"^^xsd:string), "Male"^^xsd:string,"Female"^^xsd:string) as ?Sex) .
#filter (?Sex = "Male"^^xsd:string) .
}

ORDER BY ?ID
